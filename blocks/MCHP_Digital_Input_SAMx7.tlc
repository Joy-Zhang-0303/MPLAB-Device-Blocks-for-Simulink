
%function BlockInstanceSetup_MCHP_Digital_Input(block, system) void
%assign PID = block.RTWdata.PID
%assign ::MCHP_Clock_PMC_PCR[PID] = 1 
%endfunction
%function Outputs_MCHP_Digital_Input(block, system) Output
/* MCHP_Digital_Input Block: %<Name>/Output */
%assign PORT = CAST("String",block.RTWdata.Port)
%assign PortNum = CAST("Number",block.RTWdata.PortNum)
%assign PINS = MchpVector(block.RTWdata.Pins)
%assign nPars = SIZE(PINS,1)
%assign SIMULTANEOUS = CAST("Number",block.RTWdata.Simultaneous_Flag)
%assign PACK = CAST("Number",block.RTWdata.Pack_Flag)
%switch ::ChipIdL1
%case "SAME"
%case "SAMV"
%case "SAMS"
%assign PDSR = "PIO%<PORT>_REGS->PIO_PDSR"
%break
%case "SAMRH"
%assign PDSR =  "PIO_REGS->PIO_GROUP[%<PortNum>].PIO_PDSR"
%break
%default
%error(" Digital Input: Chip %<::ChipIdL1> not supported")
%endswitch	
%if PACK
%if !LibBlockOutputSignalIsExpr(0)
%assign u = LibBlockOutputSignal(0, "", "", 0)
%<u> = %<PDSR>;		 	 	
%endif 
%else
%if SIMULTANEOUS
{
uint32_T ReadPort%<PORT> = %<PDSR>;
%foreach idx=nPars
%if %<PINS[idx]> != -1	
%assign u = LibBlockOutputSignal(idx, "", "", 0)
%<u> = ( 0 != (ReadPort%<PORT> & %< MchpHex( 1U << PINS[idx])>));	/* Read pin P%<PORT>%<PINS[idx]> */
%endif
%endforeach
}
%else
%foreach idx=nPars
%if !LibBlockOutputSignalIsExpr(idx)
%if %<PINS[idx]> != -1	
%assign u = LibBlockOutputSignal(idx, "", "", 0)
%<u> = (0 != (%<PDSR> & %< MchpHex( 1U <<PINS[idx])>)) ;	/* Read pin P%<PORT>%<PINS[idx]> */
%endif
%endif 
%endforeach
%endif
%endif
%endfunction
%function BlockOutputSignal_MCHP_Digital_Input(block,system,portIdx,ucv,lcv,idx,retType) void
%assign PORT = CAST("String",block.RTWdata.Port)
%assign PortNum = CAST("Number",block.RTWdata.PortNum)
%assign PINS = MchpVector(block.RTWdata.Pins)
%assign SIMULTANEOUS = CAST("Number",block.RTWdata.Simultaneous_Flag)
%assign PACK = CAST("Number",block.RTWdata.Pack_Flag)
%switch ::ChipIdL1
%case "SAME"
%case "SAMV"
%case "SAMS"
%assign PDSR = "PIO%<PORT>_REGS->PIO_PDSR"
%break
%case "SAMRH"
%assign PDSR =  "PIO_REGS->PIO_GROUP[%<PortNum>].PIO_PDSR"
%break
%default
%error(" Digital Input: Chip %<::ChipIdL1> not supported")			
%endswitch	
%switch retType
%case "Signal"
%if PACK
%assign retVal = "%<PDSR>"
%else
%if SIMULTANEOUS
%assign errTxt = "Cannot generate output block signal code with SIMULTANEUS option set"
%<LibBlockReportError(block,errTxt)>
%else
%assign retVal = "(0 != (%<PDSR> & %< MchpHex( 1U << PINS[portIdx] )>))"
%endif
%endif
%break
%default
%assign errTxt = "Unsupported return type: %<retType>"
%<LibBlockReportError(block,errTxt)>
%break
%endswitch
%return retVal
%endfunction
